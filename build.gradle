plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.12.2'
}

sourceCompatibility = 11
targetCompatibility = 11

description = 'Source code similarity detector'
group = 'ee.ut.similaritydetector'
version = '2.0.0'

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

mainClassName = 'ee.ut.similaritydetector.ui.App'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.python:jython-standalone:2.7.2'
    /* Pygments version 2.5.2 is the last version that supports Python 2,
    *  so it's the last version compatible with Jython,
    *  because the latter doesn't support Python 3 yet */
    implementation 'org.pygments:pygments:2.5.2'
}

javafx {
    version = '11.0.2'
    modules = ['javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.graphics', 'javafx.media']
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules
    launcher {
        noConsole = true
        mainClassName = 'ee.ut.similaritydetector.ui.App'
    }

    jpackage {
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
    }
}

tasks.runtime.doLast {
    copy {
        from("src/main/resources")
        into("$buildDir/image/bin")
    }
}
